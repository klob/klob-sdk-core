apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'
 version ="1.0.2"
int version_code=2
android {
    compileSdkVersion 22
    buildToolsVersion '22.0.1'
    resourcePrefix "klob-sdk-core_"
    defaultConfig {
        minSdkVersion 9
        targetSdkVersion 22
        versionCode version_code
        versionName version

    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
        }
    }
    lintOptions {
        abortOnError false
    }
    enforceUniquePackageName = false

}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
//latest.integration
/*    compile 'com.android.support:appcompat-v7:22.0.0'*/
    compile 'com.android.support:support-v4:22.2.1'
    compile 'com.android.support:appcompat-v7:22.2.1'
    compile 'com.readystatesoftware.systembartint:systembartint:1.0.3'
    compile 'com.squareup.okhttp:okhttp:2.5.0'
    compile 'com.nostra13.universalimageloader:universal-image-loader:1.9.4'
    compile 'de.hdodenhof:circleimageview:1.2.0'
    compile 'com.alibaba:fastjson:1.2.4'

}
def siteUrl = 'https://github.com/klob/klob-sdk-core'      // 项目的主页
def gitUrl = 'https://github.com/klob/klob-sdk-core.git'
group = "com.diandi.klob"
install {
    repositories.mavenInstaller {
        // This generates POM.xml with proper parameters
        pom {
            project {
                packaging 'jar'
                // Add your description here
                name 'klob-sdk-core'  //项目描述
                url siteUrl
                // Set your license
                licenses {
                    license {
                        name 'The Apache Software License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id 'klob'       //填写开发者基本信息
                        name 'klob'
                        email 'klobic@gmail.com'
                    }
                }
                scm {
                    connection gitUrl
                    developerConnection gitUrl
                    url siteUrl
                }
            }
        }
    }
}
tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}
task javadoc(type: Javadoc) {

    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))

}
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}
artifacts {
   // archives javadocJar
    archives sourcesJar
}
Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())
bintray {
    user = properties.getProperty("bintray.user")
    key = properties.getProperty("bintray.apikey")
    configurations = ['archives']
    pkg {
        repo = "maven"  //发布到Bintray的那个仓库里，默认账户有四个库，我们这里上传到maven库
        name = "klob-sdk-core"  //发布到Bintray上的项目名字
        websiteUrl = siteUrl
        vcsUrl = gitUrl
        licenses = ["Apache-2.0"]
        publish = true
    }
}